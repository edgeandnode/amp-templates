# Display available commands and their descriptions (default target)
default:
    @just --list

# Install dependencies
install: install-pnpm install-amp

# Ampup installation (ampctl tool to deploy anvil network dataset)
install-amp:
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v ampctl &> /dev/null; then
        echo "ampctl not found, installing..."
        curl --proto '=https' --tlsv1.2 -sSf https://ampup.sh/install | sh
        source ~/.zshenv
    else
        echo "ampctl is already installed, skipping installation"
    fi

# Package dependencies installation
install-pnpm:
    pnpm install

# Start service dependencies
up *args:
    @mkdir -p ./infra/postgres/data
    @mkdir -p ./infra/amp/data
    docker compose up -d --wait {{args}}

# Tail logs for service dependencies
logs *args:
    docker compose logs -f --tail 100 {{args}}

# Stop service dependencies
stop *args:
    docker compose stop {{args}}

# Stop all services and remove volumes
down:
    docker compose down --volumes
    @rm -rf ./infra/postgres/data
    @rm -rf ./infra/amp/data

# clean the docker images
clean:
    docker system prune --volumes

# Lint typescript code
lint-ts *args:
    pnpm lint {{args}}

# Check typescript code for type errors
check-ts:
    pnpm check

# Deploy smart contracts to anvil
deploy-contracts:
    cp .env.example .env
    forge install foundry-rs/forge-std OpenZeppelin/openzeppelin-contracts
    forge script contracts/script/InitializePortfolio.s.sol --broadcast --rpc-url http://localhost:8545

# Seed ERC20 transfer transactions between Wallet #1 and Wallet #2
seed-transfers:
    forge script contracts/script/DoTransfers.s.sol --broadcast --rpc-url http://localhost:8545

# Run all development services in parallel
[parallel]
dev: dev-watch dev-app

# Run the app in development mode
dev-app:
    pnpm dev

# Start the amp development server (watch config file changes)
dev-watch:
    pnpm amp dev

# Start service dependencies and deploy initial anvil raw dataset
setup: up deploy-anvil-dataset

# Deploy anvil raw dataset
[private]
deploy-anvil-dataset:
    ampctl manifest generate --network anvil --kind evm-rpc --out anvil.json
    ampctl dataset register _/anvil -t 0.0.1 ./anvil.json
    ampctl dataset deploy _/anvil@0.0.1